plugins {
    id 'org.springframework.boot' version '2.2.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
    id 'idea'
    id 'checkstyle'
    id 'io.freefair.lombok' version '5.1.0'
}

group = 'com.bt'
version = ''
sourceCompatibility = 11

repositories {
    mavenCentral()
}

jar {
    manifest {
        attributes 'Main-Class': 'com.bt.scrumble.ScrumbleApi'
    }
}

test {
    afterTest { desc, result ->
        logger.quiet "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }
}

tasks.withType(Test) {
    scanForTestClasses = false
    include "**/*Test.class"
}

tasks.withType(Checkstyle) {
    ignoreFailures = true
    reports {
        html.enabled = true
    }
}

dependencies {

    /*
        WEB
     */
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-webflux')
    implementation('org.springframework.boot:spring-boot-starter-websocket')

    /*
        DATABASE
     */
    implementation('org.postgresql:postgresql')
    implementation('org.flywaydb:flyway-core')
    implementation('org.springframework.boot:spring-boot-starter-jdbc')


    /*
        SECURITY
     */
    implementation('org.springframework.security.oauth.boot:spring-security-oauth2-autoconfigure:2.2.0.RELEASE')
    implementation('org.springframework.security:spring-security-oauth2-client')
    implementation('org.springframework.security.oauth:spring-security-oauth2')
    implementation('io.jsonwebtoken:jjwt:0.2')
    implementation('org.apache.commons:commons-lang3')

    /*
        UTILS
     */
    implementation('org.springframework.boot:spring-boot-starter-validation')
    annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")

    /*
        TESTING
    */
    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('io.projectreactor:reactor-test')
    testImplementation('org.springframework.security:spring-security-test')
    implementation('org.springframework.boot:spring-boot-starter-actuator')
}
